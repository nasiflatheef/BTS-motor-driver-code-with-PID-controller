#define ENCA 2 // encoderA
#define ENCB 3 // encoderB
#define RPWM 5
#define LPWM 6
#define IN2 7
#define IN1 8

volatile int posi = 0;
long prevT = 0;
float eprev = 0;
float eintegral = 0;

void setup() {
  Serial.begin(9600);
  pinMode(ENCA,INPUT);
  pinMode(ENCB,INPUT);
  attachInterrupt(digitalPinToInterrupt(ENCA),readEncoder,RISING);
  
  pinMode(RPWM,OUTPUT);
  pinMode(LPWM,OUTPUT);
  pinMode(IN1,OUTPUT);
  pinMode(IN2,OUTPUT);
  
  digitalWrite(IN1,HIGH);
  digitalWrite(IN2,HIGH);
  Serial.println("target pos");
}

void loop() {

  // set target position
  int target = 1200;
  //int target = 250*sin(prevT/1e6);

  // PID constants
  float kp = 0.5;
  float kd = 0.001;
  float ki = 0.01;

  // time difference
  long currT = micros();
  float deltaT = ((float) (currT - prevT))/( 1.0e6 );
  prevT = currT;

  // Read the position
  int pos = 0; 
  noInterrupts(); // disable interrupts temporarily while reading
  pos = posi;
  interrupts(); // turn interrupts back on
  
  // error
  int e = pos - target;

  // derivative
  float dedt = (e-eprev)/(deltaT);

  // integral
  eintegral = eintegral + e*deltaT;

  // control signal
  float u = kp*e + kd*dedt + ki*eintegral;

  // motor power
  float pwr = fabs(u);
  if( pwr > 255 ){
    pwr = 255;
  }

  // motor direction
  int dir = 1;
  if(u<0){
    dir = -1;
  }

  // signal the motor
  setMotor(dir,pwr,RPWM, LPWM);


  // store previous error
  eprev = e;

  Serial.print(target);
  Serial.print(" ");
  Serial.print(pos);
  Serial.println();
}

void setMotor(int dir, int pwmVal, int rpwm, int lpwm){
  //analogWrite(pwm,pwmVal);
  if(dir == 1){
    analogWrite(rpwm, pwmVal);
    analogWrite(lpwm, 0);

  }
  else if(dir == -1){
    analogWrite(lpwm, pwmVal);
    analogWrite(rpwm, 0);
  }
  else{
   analogWrite(LPWM, 0);
   analogWrite(RPWM, 0);
  }  
}

void readEncoder(){
  int b = digitalRead(ENCB);
  if(b > 0){
    posi++;
  }
  else{
    posi--;
  }
}
